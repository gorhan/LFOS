abstract xor objective_purpose
    maxi
    mini

abstract or time_related_objective_criteria
    makespan
    tardiness
    earliness
    lateness
    centering

abstract or resource_related_objective_criteria
    power_consumption_criteria
    utilization_criteria
    throughput_criteria


abstract or abstract_objective_criteria
    task_related_user_defined_criteria
    resource_related_user_defined_criteria

abstract task_related_objective
    task_objective_purpose : objective_purpose
    or criteria
        `time_related_objective_criteria
        `abstract_objective_criteria
            [ task_related_user_defined_criteria && ! resource_related_user_defined_criteria]

abstract resource_related_objective
    resource_objective_purpose : objective_purpose
    or criteria
        `resource_related_objective_criteria
        `abstract_objective_criteria
            [ ! task_related_user_defined_criteria && resource_related_user_defined_criteria]

abstract global_objective
    purpose : objective_purpose
    or criteria
        `time_related_objective_criteria
        `resource_related_objective_criteria
        `abstract_objective_criteria

abstract resource
    capacity
    resource_type
        xor abstraction
            ACTIVE
            PASSIVE
            COMPOSITE
        identifier
    xor mode
        shared
        or exclusive
            capacity_based
            semantic_based
    power_consumption
        xor scalable ?
            discrete
            continuous
        [scalable => resource_type.abstraction.ACTIVE]
    resource_objective : resource_related_objective ?

abstract timing
    release_time
    execution_time
    deadline
    xor periodicity
        periodic
        aperiodic
            sporadic ?
            
abstract resource_requirement
    eligible_resources -> resource +
    resource_identifier

abstract requirement
    xor deadline_requirement
        hard
        soft
        firm
    required_resources -> resource_requirement +
    [no res: required_resources.eligible_resources | res.resource_type.abstraction.COMPOSITE]

abstract task
    xor granularity
        terminal
        composite
    `timing
    priority
    preemptable ?
        cooperative ?
    `requirement
    task_objective : task_related_objective ?
    dependency ?
        token_type
        n_tokens
        sequence_dependent_setup_time

abstract scheduling_policy
    grouping ?
    xor ranking
        xor default
            ERT
            ED
            RM
            SJF
            FIFO
        user_defined

abstract scheduling_characteristic
    xor scheduling_characteristic_type
        online
        offline
    preemptive ?
    xor migration ?
        task_level
        job_level
    policy : scheduling_policy
    time_resolution
    xor priority_assignment
        fixed
        dynamic
    scheduling_window
    objective -> global_objective +
    
abstract or solver
    IBM_ILOG_CPLEX_Optimizer
    SCIP_SoPlex
    Knapsack
    SAS_OR_R

abstract DSLB
    name : string
    [name = "Domain-Specific Language Based"]

abstract DSB
    name : string
    [name = "Domain Structure Based"]

abstract or input
    `DSLB
    `DSB

abstract or output
    `DSLB
    `DSB

abstract scheduling_strategy
    `solver
    solver_meta_knowledge
    `input
    `output

abstract scheduler
    system -> resource +
    taskset -> task +
    properties : scheduling_characteristic 
    strategy : scheduling_strategy ?

// EXAMPLE-3: COMPLEX SCHEDULING APPLICATION
// Resources
cpu1 : resource
[cpu1.power_consumption.scalable.discrete && cpu1.mode.exclusive.capacity_based && !cpu1.resource_objective]
[cpu1.resource_type.abstraction.ACTIVE]
memory1 : resource
[!memory1.power_consumption.scalable && memory1.mode.exclusive.capacity_based && !memory1.resource_objective]
[memory1.resource_type.abstraction.PASSIVE]
//CPUs : resource
//[! CPUs.power_consumption.scalable && CPUs.mode.exclusive.capacity_based && CPUs.resource_type.abstraction.COMPOSITE]

// Tasks
t1 : task
req1 : resource_requirement
[ req1.eligible_resources = cpu1 ]
[t1.requirement.required_resources = req1 && !t1.dependency && !t1.task_objective && t1.timing.periodicity.periodic && t1.granularity.terminal && ! t1.preemptable && t1.requirement.deadline_requirement.soft]
t2 : task
req2 : resource_requirement
[ req2.eligible_resources = cpu1 ]
[t2.requirement.required_resources = req2 && t2.dependency && !t2.task_objective && t2.timing.periodicity.aperiodic && t2.granularity.terminal && t2.preemptable && !t2.preemptable.cooperative && t2.requirement.deadline_requirement.hard]

objective_gl : global_objective
[  objective_gl.purpose.mini &&
   !objective_gl.criteria.time_related_objective_criteria.tardiness && !objective_gl.criteria.time_related_objective_criteria.makespan && objective_gl.criteria.time_related_objective_criteria.lateness && !objective_gl.criteria.time_related_objective_criteria.earliness && !objective_gl.criteria.time_related_objective_criteria.centering && objective_gl.criteria.resource_related_objective_criteria.power_consumption_criteria && !objective_gl.criteria.resource_related_objective_criteria.utilization_criteria && !objective_gl.criteria.resource_related_objective_criteria.throughput_criteria && !objective_gl.criteria.abstract_objective_criteria]

sched : scheduler
[ sched.taskset = t1 ++ t2 ]
[ sched.system = cpu1 ++ memory1]
[ sched.properties.objective = objective_gl && sched.properties.policy.ranking.default.FIFO && !sched.properties.policy.grouping && sched.properties.scheduling_characteristic_type.offline && sched.properties.preemptive && sched.properties.priority_assignment.dynamic && sched.properties.migration.job_level]
[ !sched.strategy ]

