abstract xor objective_purpose
    maxi
    mini

abstract or time_related_objective_criteria
    makespan
    tardiness
    earliness
    lateness
    centering

abstract or resource_related_objective_criteria
    power_consumption
    utilization
    throughput

abstract or user_defined_objective_criteria
    task_related_user_defined_criteria
    resource_related_user_defined_criteria

abstract task_related_objective
    purpose : objective_purpose
    or criteria
        `time_related_objective_criteria
        `user_defined_objective_criteria
            [ task_related_user_defined_criteria && ! resource_related_user_defined_criteria]

abstract resource_related_objective
    purpose : objective_purpose
    or criteria
        `resource_related_objective_criteria
        `user_defined_objective_criteria
            [ ! task_related_user_defined_criteria && resource_related_user_defined_criteria]

abstract global_objective
    purpose : objective_purpose
    or criteria
        `time_related_objective_criteria
        `resource_related_objective_criteria
        `user_defined_objective_criteria

abstract resource
    xor granularity
        composite
        actual
    xor type
        active
        passive
        composite
        [ (active || passive) => actual ]
    dedicated ?
    capacity
    mutual_exclusion ?
        self ?
    power_consumption
        xor scalable ?
            discrete
            continuous
        [scalable => active]
    objective : resource_related_objective ?

abstract timing
    release_time
    execution_time
    deadline ?
    xor periodicity
        periodic
        aperiodic
            sporadic ?

abstract requirement
    xor deadline
        hard
        soft
        firm
    required_resource -> resource +
        [some r : resource | r.type.active]

abstract task
    xor granularity
        composite
        actual
    `timing
    objective : task_related_objective ?
    dependency ?
        data
        task
        interval ?
        [ data xor task ]
    preemptable ?
        partial ?
    `requirement
    xor priority
        fixed
        dynamic

    [requirement.deadline.hard => timing.deadline]

r1 : resource
[scalable && ! dedicated && ! mutual_exclusion && ! r1.objective.criteria.user_defined_objective_criteria]
t1 : task
[t1.requirement.required_resource = r1 && ! t1.objective && t1.timing.periodicity.periodic && t1.granularity.actual && ! t1.preemptable]
[! r1.objective]