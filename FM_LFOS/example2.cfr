abstract xor objective_purpose
    maxi
    mini

abstract or time_related_objective_criteria
    makespan
    tardiness
    earliness
    lateness
    centering

abstract or resource_related_objective_criteria
    power_consumption_criteria
    utilization_criteria
    throughput_criteria


abstract or abstract_objective_criteria
    task_related_user_defined_criteria
    resource_related_user_defined_criteria

abstract task_related_objective
    purpose : objective_purpose
    or criteria
        `time_related_objective_criteria
        `abstract_objective_criteria
            [ task_related_user_defined_criteria && ! resource_related_user_defined_criteria]

abstract resource_related_objective
    purpose : objective_purpose
    or criteria
        `resource_related_objective_criteria
        `abstract_objective_criteria
            [ ! task_related_user_defined_criteria && resource_related_user_defined_criteria]

abstract global_objective
    purpose : objective_purpose
    or criteria
        `time_related_objective_criteria
        `resource_related_objective_criteria
        `abstract_objective_criteria

abstract resource
    capacity
    resource_type
        xor abstraction
            ACTIVE
            PASSIVE
            COMPOSITE
        identifier
    xor mode
        shared
        or exclusive
            capacity_based
            semantic_based
    power_consumption
        xor scalable ?
            discrete
            continuous
        [scalable => resource_type.abstraction.ACTIVE]
    objective : resource_related_objective ?

abstract timing
    release_time
    execution_time
    deadline
    xor periodicity
        periodic
        aperiodic
            sporadic ?
            
abstract resource_requirement
    eligible_resources -> resource +
    resource_identifier

abstract requirement
    xor deadline_requirement
        hard
        soft
        firm
    required_resources -> resource_requirement +
    [no res: required_resources.eligible_resources | res.resource_type.abstraction.COMPOSITE]

abstract task
    xor granularity
        terminal
        composite
    `timing
    priority
    preemptable ?
        cooperative ?
    `requirement
    objective : task_related_objective ?
    dependency ?
        token_type
        n_tokens
        sequence_dependent_setup_time

abstract scheduling_policy
    grouping ?
    xor ranking
        xor default
            ERT
            ED
            RM
            SJF
            FIFO
        user_defined

abstract scheduling_characteristic
    xor scheduling_characteristic_type
        online
        offline
    preemptive ?
    xor migration ?
        task_level
        job_level
    policy : scheduling_policy
    time_resolution
    xor priority_assignment
        fixed
        dynamic
    scheduling_window
    objective -> global_objective +
    
abstract or solver
    IBM_ILOG_CPLEX_Optimizer
    SCIP_SoPlex
    Knapsack
    SAS_OR_R

abstract DSLB
    name : string
    [name = "Domain-Specific Language Based"]

abstract DSB
    name : string
    [name = "Domain Structure Based"]

abstract or input
    `DSLB
    `DSB

abstract or output
    `DSLB
    `DSB

abstract scheduling_strategy
    `solver
    solver_meta_knowledge
    `input
    `output

abstract scheduler
    system -> resource +
    taskset -> task +
    properties : scheduling_characteristic 
    strategy : scheduling_strategy ?

// EXAMPLE-2: TRAVELING SALESMAN SCHEDULING APPLICATION
machine: resource
[ machine.resource_type.abstraction.ACTIVE && machine.mode.exclusive.capacity_based && !machine.power_consumption.scalable && !machine.objective ]

city: task
task_req: resource_requirement
[ task_req.eligible_resources = machine ]
[ city.timing.periodicity.aperiodic && !city.timing.periodicity.aperiodic.sporadic && city.requirement.deadline_requirement.soft && city.requirement.required_resources = task_req && city.granularity.terminal && !city.objective && city.dependency ]

tsp_scheduler: scheduler
mini_objective: global_objective
[ mini_objective.purpose.mini &&
    !mini_objective.criteria.time_related_objective_criteria.tardiness &&
    !mini_objective.criteria.time_related_objective_criteria.centering &&
    !mini_objective.criteria.time_related_objective_criteria.earliness &&
    !mini_objective.criteria.resource_related_objective_criteria &&
    !mini_objective.criteria.abstract_objective_criteria]
[ tsp_scheduler.taskset = city && tsp_scheduler.system = machine ]
[ tsp_scheduler.properties.scheduling_characteristic_type.offline && tsp_scheduler.properties.policy.ranking.user_defined && tsp_scheduler.properties.priority_assignment.fixed && tsp_scheduler.properties.objective = mini_objective ]
[ !tsp_scheduler.strategy ]
